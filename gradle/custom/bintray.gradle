/**
 * Configuration for uploading artifacts to Bintray JCenter repository
 */

apply plugin: 'com.jfrog.bintray'
apply plugin: 'maven-publish'

task javadocJar(type: Jar, dependsOn: javadoc) {
	classifier = 'javadoc'
	from 'build/docs/javadoc'
}

task sourcesJar(type: Jar) {
	from sourceSets.main.allSource
	classifier = 'sources'
}

def pomConfig = {
	resolveStrategy = Closure.DELEGATE_FIRST

	licenses {
		license {
			name 'The Apache Software License, Version 2.0'
			url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
			distribution 'repo'
		}
	}
	developers {
		developer {
			id 'nikolaylagutko'
			name 'Nikolay Lagutko'
			email 'NikolayLagutko@mail.com'
		}
	}
}

publishing {
	publications {
		mavenJava(MavenPublication) {
			from components.java
			artifact sourcesJar
			artifact javadocJar
			
			pom.withXml {
				asNode().children().last() + pomConfig
			}
		}
	} 
}

bintray {
	user = 'nikolaylagutko'
	key = System.env.BINTRAY_API_KEY
	
	publications = ['mavenJava']
	
	pkg {
		repo = 'spock-extensions'
		name = 'spock-injectmocks'
		desc = 'Mockito\'s InjectMocks alternative for Spock'
		licenses = ['Apache-2.0']
	}
}

task install(dependsOn: 'publishMavenJavaPublicationToMavenLocal') << {
    logger.info "Installing $project.name"
}

bintrayUpload.dependsOn install
